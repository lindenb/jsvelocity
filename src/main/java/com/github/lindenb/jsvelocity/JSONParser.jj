/**
 * Author:
 * 	Pierre Lindenbaum PhD
 * Date:
 * 	March-2011
 * Contact:
 * 	plindenbaum@yahoo.fr
 * Reference:
 *   
 * WWW:
 * 	http://plindenbaum.blogspot.com
 * Wiki
 *  
 * Motivation:
 * 	process JSON structure with apache velocity
 */
options {
STATIC=false;
}


PARSER_BEGIN(JSONParser)

package com.github.lindenb.jsvelocity;
import java.util.*;
import java.io.*;
import java.math.*;

@SuppressWarnings("unchecked")
public class JSONParser
{
public static interface JSNode
	{
	public boolean isArray();
	public boolean isObject();
	public boolean isNumber();
	public boolean isString();
	public boolean isBoolean();
	public boolean isNull();
	public JSNode getParentNode();
	public void setParentNode(JSNode p);
	}

public static class JSArray
	extends java.util.ArrayList<JSNode>
	implements JSNode
	{
	JSNode parent=null;
	public boolean isArray() { return true;}
	public boolean isObject() { return false;}
	public boolean isNumber() { return false;}
	public boolean isString() { return false;}
	public boolean isBoolean() { return false;}
	public boolean isNull() { return false;}
	public JSNode getParentNode()
		{
		return parent;
		}
	public void setParentNode(JSNode parent)
		{
		this.parent=parent;
		}
	}

public static class JSObject
	extends java.util.LinkedHashMap<String,JSNode>
	implements JSNode
	{
	JSNode parent=null;
	public boolean isArray() { return false;}
	public boolean isObject() { return true;}
	public boolean isNumber() { return false;}
	public boolean isString() { return false;}
	public boolean isBoolean() { return false;}
	public boolean isNull() { return false;}
	public JSNode getParentNode()
		{
		return parent;
		}
	public void setParentNode(JSNode parent)
		{
		this.parent=parent;
		}
	}
	
public static class JSNull
	implements JSNode
	{
	JSNode parent;
	public boolean isArray() { return false;}
	public boolean isObject() { return true;}
	public boolean isNumber() { return false;}
	public boolean isString() { return false;}
	public boolean isBoolean() { return false;}
	public boolean isNull() { return true;}
	public JSNode getParentNode()
		{
		return parent;
		}
	public String toString()
		{
		return null;
		}
	public void setParentNode(JSNode parent)
		{
		this.parent=parent;
		}
	}	

public static class JSGenericNode<T>
	implements JSNode
	{
	JSNode parent=null;
	T value;
	
	JSGenericNode(T value)
		{
		this.value=value;
		}
	public boolean isArray() { return false;}
	public boolean isObject() { return false;}
	public boolean isNumber() { return value instanceof Number;}
	public boolean isString() { return value instanceof String;}
	public boolean isBoolean() {  return value instanceof Boolean;}
	public boolean isNull() { return false;}
	public JSNode getParentNode()
		{
		return parent;
		}
	
	public T getValue()
		{
		return value;
		}
	
	public String toString()
		{
		return value.toString();
		}
		
	public void setParentNode(JSNode parent)
		{
		this.parent=parent;
		}
	}

private String unescape(String s) throws ParseException
	{
	 StringBuilder b=new StringBuilder(s.length());
        int i=0;
        while(i<s.length())
        	{
        	if(s.charAt(i)=='\\')
        		{
        		if( i+1== s.length())  throw new ParseException("Badly escaped string "+s);
        		++i;
        		switch(s.charAt(i))
        			{
        			case 'n': b.append("\n");break;
        			case 'r': b.append("\r");break;
        			case 't': b.append("\t");break;
        			case '\\': b.append("\\");break;
        			case '\'': b.append("\'");break;
        			case '\"': b.append("\"");break;
        			default: throw new ParseException("Badly escaped string "+s);
        			}
        		}
        	else
        		{
        		b.append(s.charAt(i));
        		}
        	++i;
        	}
        return b.toString();
	}
}


PARSER_END(JSONParser)

SKIP :
{
" "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n"])* "\n">
| <"/*">: INSIDE_COMMENT
}

<INSIDE_COMMENT>
SKIP:
{
        <"*/">: DEFAULT
|       <~[]>
}



TOKEN : /* LITERALS */
{
  <TRUE: "true">
| <FALSE: "false">
| <NULL: "null">
| <#LETTER: ["_","a"-"z","A"-"Z"] >
| <#DIGIT: ["0"-"9"] >
| <#SIGN: ["-","+"]>
| <#EXPONENT: ("E"|"e") (<SIGN>)? (<DIGIT>)+ >
| <FLOATING_NUMBER: (<DIGIT>)* "." (<DIGIT>)* (<EXPONENT>)?
| (<DIGIT>)+ (<EXPONENT>) >
| <INT_NUMBER: (<DIGIT>)+ >
| <IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>|"-")* >
| <#ESCAPE_CHAR: "\\" ["n","t","b","r","f","\\","'","\""] >
| <SIMPLE_QUOTE_LITERAL:
"\'"
( (~["\'","\\","\n","\r"])
| <ESCAPE_CHAR>
)*
"\'"
>
|
<DOUBLE_QUOTE_LITERAL:
"\""
( (~["\"","\\","\n","\r"])
| <ESCAPE_CHAR>
)*
"\""
>
| <ARRAY_OPEN: "[">
| <ARRAY_CLOSE: "]">
| <COMMA: ",">
| <OBJECT_OPEN: "{">
| <OBJECT_CLOSE: "}">
| <PAR_OPEN: "(">
| <PAR_CLOSE: ")">
| <COLON: ":">
}

public JSNode parse():{JSNode o;}
	{
	o=any() eof()
		{
		return o;
		}
	}

private JSNode any():
	{JSNode o;}
	{
	(
	o= complexNode()
	| o= terminalNode()
	)
	{return o;}
	}

public JSNode complexNode():
	{JSNode o;}
	{
	(
	o= objectNode()
	| o= arrayNode()
	)
	{return o;}
	}

public JSNode terminalNode():
	{
	JSNode n;
	}
	{
	(
	  n=decimalNode() { return n;}
	| n=intNode() { return n;}
	| n=boolNode() { return n;}
	| n=nilNode() { return n;}
	| n=stringNode() { return n;}
	)
	}

public JSNull nilNode():{}
	{
	<NULL> { return new JSNull(); }
	}

public JSGenericNode<Boolean> boolNode():{}
	{
	(
	  <TRUE>  { return new JSGenericNode<Boolean>(Boolean.TRUE); }
	| <FALSE> { return new JSGenericNode<Boolean>(Boolean.FALSE);}
	)
	}

public JSGenericNode<Number> intNode():{	Token t;}
	{
	t=<INT_NUMBER> { return new JSGenericNode<Number>(new Integer(t.image)); }
	}

public JSGenericNode<Number> decimalNode():{	Token t;}
	{
	t=<FLOATING_NUMBER> {return new JSGenericNode<Number>(new Double(t.image)); }
	}

public JSGenericNode<String> stringNode():{	String s;}
	{
	s=characters() { return new JSGenericNode<String>(s); }
	}


private String characters():
	{
	Token t;
	}
	{
	(
	t=<SIMPLE_QUOTE_LITERAL>
		{
		return  unescape(t.image.substring(1,t.image.length()-1));
		}
	| t=<DOUBLE_QUOTE_LITERAL>
		{
		return unescape(t.image.substring(1,t.image.length()-1));
		}
	| t=<IDENTIFIER>
	     {
	     return t.image;
	     }
	)
	}

public JSArray arrayNode():
	{ JSArray vector= new JSArray(); JSNode o;}
	{
	openArray() ( o=any() {o.setParentNode(vector);vector.add(o);} (comma() o=any() {o.setParentNode(vector);vector.add(o);} ) * )? closeArray()
		{
		return vector;
		}
	}

public JSObject objectNode():
	{JSObject map= new JSObject(); }
	{
	openObject() ( keyValue(map) (comma() keyValue(map))*)? closeObject()
		{
		return map;
		}
	}

public void colon():
   {
   }
   {
   <COLON>
   }

private void keyValue( JSObject map):
	{String k; JSNode v;}
	{
	(k=characters() colon() v=any())
		{
		if(map.containsKey(k)) throw new ParseException(k+" defined twice in object");
		v.setParentNode(map);
		map.put(k,v);
		}
	}
	
/** function for streaming big object/array */
public void openObject():{}
	{
	<OBJECT_OPEN>
	}

/** function for streaming big object/array */
public void closeObject():{}
	{
	<OBJECT_CLOSE>
	}

/** function for streaming big object/array */
public void openArray():{}
	{
	<ARRAY_OPEN>
	}

/** function for streaming big object/array */
public void closeArray():{}
	{
	<ARRAY_CLOSE>
	}

/** function for streaming big object/array */
public void comma():{}
	{
	<COMMA>
	}

/** go to EOF, check that there are only some blanks chars */
public void eof():{}
	{
	<EOF>
	}	
	

